#!/bin/zsh
# Node.js-related functions

_pm_detect() # Detect which package manager to use
{
  if [[ -f bun.lock || -f bun.lockb ]]; then
    echo "bun"
  elif [[ -f pnpm-lock.yaml ]]; then
    echo "pnpm"
  elif [[ -f yarn.lock ]]; then
    echo "yarn"
  elif [[ -f package-lock.json ]]; then
    echo "npm"
  else
    echo "pnpm"
  fi
}

pm() # Determine package manager and run command with it
{
  local pkg_manager=$(_pm_detect)
  command "$pkg_manager" "$@"
}

nrs() # Interactively run npm scripts with FZF
{ 
    local package_json="package.json"
    
    if [[ ! -f "$package_json" ]]; then
        echo "Error: package.json not found in current directory"
        return 1
    fi
    
    local selected_script=$(
        jq -r '.scripts | keys[]' "$package_json" \
        | sort \
        | fzf --no-preview -q "$1"
    )
    
    if [[ -n "$selected_script" ]]; then
        pm run "$selected_script"
    fi
}

node-flush() # Reinstall node deps
{
    rm -rf node_modules/ build package.lock
    pm i && pm start
}

nui() # Interactive package updates (pnpm, npm, bun)
{
  local pm=$(_pm_detect)
  case "$pm" in
      bun)
        bun update --interactive
        ;;
      pnpm)
        pnpm update --interactive || pnpm dlx npm-check-updates --interactive || bunx npm-check-updates --interactive || bun x npm-check-updates --interactive || npx npm-check-updates --interactive
        ;;
      npm)
        bunx npm-check-updates --interactive || bun x npm-check-updates --interactive || pnpm dlx npm-check-updates --interactive || npx npm-check-updates --interactive
        ;;
      *)
        pm update --interactive
        ;;
  esac
}

nua() # Update all packages to latest (pnpm, npm, bun)
{
  local pm=$(_pm_detect)
  case "$pm" in
      bun)
        bun update --latest
        ;;
      pnpm)
        pnpm update --latest || { pnpm dlx npm-check-updates -u && pnpm install; } || { bunx npm-check-updates -u && pnpm install; } || { bun x npm-check-updates -u && pnpm install; } || { npx npm-check-updates -u && pnpm install; } || pnpm update
        ;;
      npm)
        { bunx npm-check-updates -u && npm install; } || { bun x npm-check-updates -u && npm install; } || { pnpm dlx npm-check-updates -u && npm install; } || { npx npm-check-updates -u && npm install; } || npm update
        ;;
      *)
        pm update --latest
        ;;
  esac
}

#====================
# NVM
#====================

nvm() {
  unset -f nvm
  [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh" --no-use # This loads nvm
  nvm "$@"
}

# This loads nvm bash_completion
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"

load-nvmrc() {
  local nvmrc_path=$(nvm_find_nvmrc) || return
  local current_version=$(nvm current)
  local default_version=$(nvm version default)

  if [[ -n "$nvmrc_path" ]]; then
    local nvmrc_version=$(cat "$nvmrc_path")

    # Only switch if different
    if [[ "$current_version" != "$nvmrc_version" ]]; then
      nvm use "$nvmrc_version" --silent
    fi
  else
    # No .nvmrc â†’ revert to default
    if [[ "$current_version" != "$default_version" ]]; then
      nvm use default --silent
    fi
  fi
}

load-nvmrc-deferred() {
  zsh-defer -c load-nvmrc
}
