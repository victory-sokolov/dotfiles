#!/bin/zsh

typeset -g _DOTENV_FILE=""
typeset -g _DOTENV_MTIME=""

dotenv_select_file() {
  for f in .env.local .env.development .env; do
    [[ -f $f ]] && echo "$PWD/$f" && return
  done
}

dotenv_load() {
  local file="$1"

  # shellcheck disable=SC1090
  set -a
  source "$file"
  [[ -f .env.keys ]] && source .env.keys
  set +a

  _DOTENV_FILE="$file"
  _DOTENV_MTIME=$(stat -f %m "$file" 2>/dev/null)

  echo "✓ Loaded: ${file:t}"
}

dotenv_unload() {
  [[ -z $_DOTENV_FILE ]] && return

  # Unset vars from old env file
  while IFS='=' read -r key _; do
    [[ -n $key && $key != \#* ]] && unset "$key"
  done < "$_DOTENV_FILE"

  echo "✓ Unloaded: ${_DOTENV_FILE:t}"

  _DOTENV_FILE=""
  _DOTENV_MTIME=""
}

dotenv_load_file() {
  local file="$1"
  
  if [[ -z "$file" ]]; then
    echo "Usage: dotenv_load_file <path_to_env_file>"
    return 1
  fi
  
  if [[ ! -f "$file" ]]; then
    echo "Error: File '$file' not found"
    return 1
  fi
  
  # Convert to absolute path if relative
  if [[ "$file" != /* ]]; then
    file="$PWD/$file"
  fi
  
  dotenv_unload
  dotenv_load "$file"
}

dotenv_check() {
  local file mtime
  file=$(dotenv_select_file)

  # Previously loaded file removed
  if [[ -n "$_DOTENV_FILE" && ! -f "$_DOTENV_FILE" ]]; then
    echo "⚠️  env file removed: ${_DOTENV_FILE:t}"
    dotenv_unload
    return
  fi

  # No env file in this directory
  if [[ -z "$file" ]]; then
    dotenv_unload
    return
  fi

  mtime=$(stat -f %m "$file" 2>/dev/null)

  # New file or changed file
  if [[ "$file" != "$_DOTENV_FILE" || "$mtime" != "$_DOTENV_MTIME" ]]; then
    if [[ -n "$_DOTENV_FILE" ]]; then
      if [[ "$file" == "$_DOTENV_FILE" ]]; then
        echo "↻ env file changed: ${file:t}"
      else
        echo "↻ env file switched: ${file:t}"
      fi
    fi

    dotenv_unload
    dotenv_load "$file"
  fi
}
